plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.google.protobuf' version '0.8.17'
}

javafx {
    version = JavaVersion.VERSION_11
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

group 'ecdar'
version '2.1'

mainClassName = 'ECDAR'

sourceCompatibility = JavaVersion.VERSION_11

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'ecdar.Ecdar'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.41.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.17.2'
def protocVersion = protobufVersion

dependencies {
    implementation fileTree(dir: 'src/libs', include: ['*.jar'])
    testImplementation group: 'junit', name: 'junit', version: '4.11'
    implementation 'com.jfoenix:jfoenix:9.0.9'
    implementation group: 'de.codecentric.centerdevice', name: 'javafxsvg', version: '1.0.0'
    implementation group: 'com.github.jiconfont', name: 'jiconfont-javafx', version: '1.0.0'
    implementation group: 'com.github.jiconfont', name: 'jiconfont-google_material_design_icons', version: '2.2.0.1'
    implementation 'org.kordamp.ikonli:ikonli-core:2.3.0'
    implementation 'org.kordamp.ikonli:ikonli-material-pack:2.3.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'org.springframework:spring-core:5.0.3.RELEASE' // Used for evaluating expressions
    implementation 'org.springframework:spring-expression:5.0.3.RELEASE' // Used for evaluating expressions
    implementation 'com.bpodgursky:jbool_expressions:1.13' // Used for converting boolean expressions to disjunctive normal form
    implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.5'
    compile "com.google.protobuf:protobuf-java:3.18.0"

    //GRPC:
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    generated {
        java.srcDir 'src/main/java/'
    }

    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}